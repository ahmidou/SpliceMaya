global proc addSpliceDeformer()
{
  string $sel[];
  $sel = `ls -selection -exactType transform`;
  $d=`deformer -type "spliceMayaDeformer"`;
  select -r $d;
  int $i = 0;
  for($obj in $sel){
    fabricSplice("addIOPort", $d, "{\"portName\":\""+("mesh"+$i)+"\", \"dataType\":\"PolygonMesh\", \"addSpliceMayaAttr\":true}");
    $i += 1;
  }
}

global proc FabricSpliceLoadMenu(string $pluginName)
{
  global string $fabricSpliceMenu;
  if (`menu -exists $fabricSpliceMenu`)
     deleteUI $fabricSpliceMenu;

  string $name = "Fabric:Splice";
  global string $gMainWindow;
  $fabricSpliceMenu = `menu -p $gMainWindow -to true -l $name`;

  // setup hotkeys
  string $toolCmd = "fabricSpliceToolActivate";
  if( !`runTimeCommand -q -ex $toolCmd` ) {
    runTimeCommand -annotation "FabricSplice Tool" -command "source \"FabricSpliceTool.mel\"; fabricSpliceActivateTool();" -category "Tools" $toolCmd;
    hotkey -k "`" -name $toolCmd;
  }

  // menu items
  menuItem -p $fabricSpliceMenu -l "Splice Editor" -c ("source \"FabricSpliceUI.mel\"; showSpliceEditor(\"" + $pluginName + "\");");
  menuItem -p $fabricSpliceMenu -divider true;
  menuItem -p $fabricSpliceMenu -l "Create Splice Node" -c ("createNode \"spliceMayaNode\";");
  menuItem -p $fabricSpliceMenu -l "Create Splice Deformer" -c ("addSpliceDeformer();");
  menuItem -p $fabricSpliceMenu -l "Create Splice Debugger" -c ("createNode \"spliceMayaDebugger\";");
  menuItem -p $fabricSpliceMenu -divider true;
  menuItem -p $fabricSpliceMenu -l "Export Splice Node" -c ("source \"FabricSpliceTool.mel\"; fabricSpliceSaveSplice();");
  menuItem -p $fabricSpliceMenu -l "Import Splice Node" -c ("source \"FabricSpliceTool.mel\"; fabricSpliceLoadSplice();");
  menuItem -p $fabricSpliceMenu -divider true;
  menuItem -p $fabricSpliceMenu -l "Enable Tool" -c $toolCmd;
  menuItem -p $fabricSpliceMenu -divider true;
  menuItem -p $fabricSpliceMenu -l "Toggle Renderer" -c ("fabricSplice \"toggleRenderer\";");
  menuItem -p $fabricSpliceMenu -divider true;
  menuItem -p $fabricSpliceMenu -l "Online Help" -c ("launch -web \"http://documentation.fabricengine.com/Splice/latest/HTML/\";");
  menuItem -p $fabricSpliceMenu -l "ThirdParty Licenses" -c ("launch -web \"http://documentation.fabricengine.com/Splice/latest/HTML/thirdpartylicenses.html\";");
  menuItem -p $fabricSpliceMenu -divider true;
  menuItem -p $fabricSpliceMenu -l "Start Profiling" -c ("fabricSplice \"startProfiling\";");
  menuItem -p $fabricSpliceMenu -l "End Profiling" -c ("fabricSplice \"stopProfiling\";");
}

global proc FabricSpliceUnloadMenu()
{
  global string $fabricSpliceMenu;
  if (`menu -exists $fabricSpliceMenu`)
    deleteUI $fabricSpliceMenu;
}
